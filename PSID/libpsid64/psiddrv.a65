;   psid64 - create a C64 executable from a PSID file
;   Copyright (C) 2001-2014  Roland Hermans <rolandh@users.sourceforge.net>

;	modified for integration into the Sidekick64 software

;
;   This program is free software; you can redistribute it and/or modify
;   it under the terms of the GNU General Public License as published by
;   the Free Software Foundation; either version 2 of the License, or
;   (at your option) any later version.
;
;   This program is distributed in the hope that it will be useful,
;   but WITHOUT ANY WARRANTY; without even the implied warranty of
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;   GNU General Public License for more details.
;
;   You should have received a copy of the GNU General Public License
;   along with this program; if not, write to the Free Software
;   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
;
;
;   The relocating PSID driver is based on a reference implementation written
;   by Dag Lem, using Andre Fachat's relocating cross assembler, xa. The
;   original driver code was introduced in VICE 1.7.
;
;   Please note that this driver code is optimized to squeeze the minimal
;   driver (without screen support) in just two memory pages. For this reason
;   it contains some strange branches to gain a few bytes. Look out for side
;   effects when updating this code!

	.fopt 0, "psiddrv.o65", 0
	.fopt 3, "Dag Lem <resid@nimrod.no>", 0

	.align 256

#ifdef SCREEN
#define CLOCK_OFFSET			(531-40)
#define BAR_X				15
#define BAR_OFFSET			(520 + BAR_X)
#define BAR_WIDTH			19
#define BAR_PIXELS			(BAR_WIDTH * 8)
#define LENGTH_OFFSET			555
#define BAR_SPRITE_SCREEN_OFFSET	$0300
#define SCROLLER_OFFSET			840
#endif
	jmp cold
	jmp setiomap

	; Parameters
init	rts
	.word 0
play	rts
	.word 0
playmax	.byte 0
speed	.word 0, 0
irqvec	.byte 0
initiomap .byte 0
playiomap .byte 0

	; Variables
playnum	.byte 0
video	.byte 0
random	.byte 0
#ifdef SCREEN
d011	.byte $1b
prevjoy	.byte 0
clkcounter .byte 0
barcounter .word 0			; progress bar increment counter
bartpi	.word 0				; progress bar ticks per increment
barpos	.byte 0				; progress bar position
#ifdef SMOOTH_SCROLL
d016	.byte $08
#endif /* SMOOTH_SCROLL */
#endif /* SCREEN */

	;Constants
#ifdef SCREEN
clkoffs	.byte 0,1,3,4
#endif /* SCREEN */

	; User interrupt vectors
irqusr	.word irqret
brkusr	.word brkjob
nmiusr	.word nmijob

	; IRQ handler
irqjob	lda $d020			; save values
	pha
	lda $dc00
	pha
#ifdef SCREEN
	lda #$fd			; test for shift lock or shift left
	sta $dc00
	lda $dc01
	bmi noshift
	lda #<COL_RASTER_TIME		; show rastertime used by play
	sta $d020
#endif /* SCREEN */
noshift	lda $01
	pha
	lda playiomap
	sta $01
	lda #0
	jsr play
	pla
	sta $01
#ifdef SCREEN
	lda #$ff
	sta $dc00
	lda $dc00			; test for joystick's fire button
	and #$10
	beq ffwd
#endif /* SCREEN */
	lda #$7f			; test for arrow left key
	sta $dc00
	lda $dc01
	and #2
	bne done
ffwd	inc $d020			; flash $d020 during fast forward
#ifdef SCREEN
	jsr clock
#endif /* SCREEN */
	jmp noshift
done	pla
	sta $dc00
	pla
	sta $d020
	lda $d019
	sta $d019
irqret	lda $dc0d
	pla
	tay
	pla
	tax
	pla
nmijob	rti

	; Cold start
	; Parameters passed in from boot loader
cold	pla
	sta random	; seed for random number generator
	pla
	sta video	; pal/ntsc flag (0=ntsc, 1=pal)
	pla
start	sta playnum	; song number
restart	sei
	cld
	ldx #$ff
	txs
	lda $01		; enable I/O without changing basic and kernal rom bank
	and #3
	bne haveio
	lda #$01
haveio	ora #$34
	sta $01
	jsr stop
#ifdef SCREEN
	; Display current song number with leading spaces
	lda #>screen_songnum+2
	beq nosongnum
	ldx playnum
	inx
	txa
	ldy #0
	ldx #$30			; '0'
	cmp #100
	bcc lt100
ldiv100	sbc #100
	inx
	cmp #100
	bcs ldiv100
	stx screen_songnum+2		; here always y == 0
	iny
	ldx #$30
	bne adiv10			; bra
lt100	cmp #10
	bcc lt10
ldiv10	sbc #10
	inx
adiv10	cmp #10
	bcs ldiv10
	pha
	txa
	sta screen_songnum+2,y
	iny
	pla
lt10	ora #$30
	sta screen_songnum+2,y
	lda #$29			; ')'
spfill	sta screen_songnum+1+2,y
	lda #$20			; ' ' (used to erase old song number)
	iny
	cpy #3
	bcc spfill
nosongnum

	ldx #4				; reset clock
	lda #$30			; '0'
rstclk	ldy clkoffs-1,x
	sta screen+CLOCK_OFFSET,y
	dex
	bne rstclk
	stx clkcounter
	stx barcounter			; reset playback progress bar
	stx barcounter+1
	stx barpos
	jsr drawbar

#endif /* SCREEN */

	; Set interrupt vectors
	ldx irqvec
	bmi vicras
store03	lda irqusr,x
	sta $0314,x
	dex
	bpl store03

	; Set VICII raster to line 0
vicras
#ifdef SCREEN
	lda d011
#else /* !SCREEN */
	lda #$0b			; blank screen
#endif /* SCREEN */
	ldx #$00
	sta $d011
	stx $d012

	; Set CIA 1 Timer A to 60Hz
	lda video
	beq ntsc
pal	lda #$25
	ldx #$40
	bne timer
ntsc	lda #$95
	ldx #$42
timer	sta $dc04
	stx $dc05

	; Get song number
	ldy playnum
	cpy playmax
	bcc songset
	ldy #$00
songset	tya
	pha

#ifdef SCREEN
	lda #>songlengths_min
	beq nosonglen
	lda songlengths_min,y		; minutes part of song length as BCD
	ldx #0
	jsr wrsonglen
	lda songlengths_sec,y		; seconds part of song length as BCD
	inx
	jsr wrsonglen
	lda songtpi_lo,y		; ticks per increment of progress bar
	sta bartpi			; to simplify pal/ntsc support there are
	lda songtpi_hi,y		; always 300 ticks per second
	sta bartpi+1
nosonglen
#endif /* SCREEN */


	; RSIDs use default environment of CIA1 only
        lda initiomap
        beq ciainit

	; Get shift number for speed bit
	cpy #32
	bcc shftset
	ldy #31

	; Find speed for current song number
shftset	lda #$00
	tax
	sec
shift	rol
	bcc nxtshft
	rol
	inx
nxtshft	dey
	bpl shift
	and speed,x
	bne ciainit

	; Enable VICII raster interrupt
vicinit	lda #$81
	sta $d01a
	bne doinit

	; Enable CIA 1 timer A interrupt
ciainit	lda #$81
	sta $dc0d

	; always enable timer A for random numbers
doinit	lda #$01
	sta $dc0e

	; If play address, override default irq vector so
	; we reach our routine to handle play routine
	lda playiomap
	beq noplay
	lda #<irqjob
	sta $0314

	; Set I/O map and call song init routine
noplay	lda #$2f
	sta $00
	lda initiomap
	bne setbank
	; Only release interrupt mask for real
	; C64 tunes (initiomap = 0) thus
	; providing a more realistic environment
	lda #$37
	; cli dosen't come into effect until
	; after the sta!
	cli
setbank	sta $01

	; get song number
	pla
	ldx random
rndwait	ldy #0
	dex
	bne rndwait
	jsr init
setiomap
	lda initiomap
	beq idle
	lda playiomap
	beq run
	lda #$37
	sta $01
	bne run

brkjob	ldx #$ff
	txs
run	cli
idle	jsr delay
	php
	sei
	jsr keyboard
#ifdef SCREEN
	jsr joystick
	lda #$7f			; check for reset (control-cbm-delete)
	sta $dc00
	lda $dc01
;	and #$24 ; CBM and CTRL
	and #$20 ; ONLY CBM
	bne noreset			; control and cbm key not pressed
	;ldx #$fe
	;stx $dc00
	;lda $dc01
	;lsr
	;bcs noreset			; delete key not pressed
	jsr stop
	lda #$37			; set bankreg
	sta $01
	lda #$22
	sta $df11
	lda #$44
	sta $df33
	;infiloop
	;jmp infiloop
	;sta $8004			; break a possible CBM80 vector
	;jmp ($fffc)			; reset
noreset
#endif /* SCREEN */
	plp
#ifdef SCREEN
	lda d011
	and #$10
	beq noscr
	jsr colorline
	jsr scroller
	jsr colortext
noscr
	jsr clock
#endif /* SCREEN */
	jmp idle

stop	.(
	ldx #0				; stop NMI and IRQ interrupts
	stx $d01a
	lda $d019
	sta $d019
	lda #$7f
	sta $dc0d
	sta $dd0d
	lda $dc0d
	lda $dd0d
	lda #8
	sta $dc0e
	sta $dd0e
	sta $dc0f
	sta $dd0f
#ifdef SCREEN
	ldy #14				; set test bit to shut down SID voices
settest	sta $d404,y
	sta sid2base+$04,y
	sta sid3base+$04,y
	dey
	dey
	dey
	dey
	dey
	dey
	dey
	bpl settest
#else /* SCREEN */
	sta $d404			; set test bit to shut down SID voices
	sta $d40b
	sta $d412
#endif /* SCREEN */
	txa				; clear SID registers
	ldx #$17
clrsid	sta $d400,x
	sta sid2base,x
#ifdef SCREEN
	sta sid3base,x
#endif /* SCREEN */
	dex
	bpl clrsid
#ifdef SCREEN
	stx clkcounter
#endif /* SCREEN */
	lda #$0f			; maximum volume
	sta $d418
#ifdef SCREEN
	sta sid2base+$18
	sta sid3base+$18
#endif /* SCREEN */
	rts
	.)

delay	.(
#ifdef SCREEN
l1	inc random			; random number generator
	lda $d012
	bpl l1				; waits until raster > $80
l2
#ifdef SMOOTH_SCROLL
	ldx d016			; smooth scroll
	sec
	sbc #$cb
	cmp #$18
	bcc l3
	ldx #8
l3	stx $d016
#endif /* SMOOTH_SCROLL */
	lda $d012
	bmi l2				; waits until raster < $80 or > $100
#ifdef SMOOTH_SCROLL
	lda #8
	sta $d016
#endif /* SMOOTH_SCROLL */
	rts
#else /* !SCREEN */
	ldx #14				; wait approx. 1 frame. accuracy is not
l1	dey				; important as we only have to deal with
	bne l1				; keyboard handling
	dex
	bne l1
	rts
#endif /* SCREEN */
	.)

keyboard	.(
	ldx key
	bmi nopress
	lda keyrow,x			; check if the key is still pressed
	sta $dc00
	lda $dc01
	and keycol,x
	beq exit			; wait until key has been released
nopress	lda playmax
#ifdef SCREEN
	clc
	adc #3				; inst/del, + and - key
#endif /* SCREEN */
	cmp #numkeys
	bcc maxnum
	lda #numkeys-1
maxnum	tax
loop	lda keyrow,x
	sta $dc00
	lda $dc01
	and keycol,x
	beq found
	dex
	bpl loop
	stx key
exit	rts
found	stx key
	txa
	bne tglscr
	jmp stop			; run/stop key pressed
tglscr
#ifdef SCREEN
	dex
	bne incsong
	lda d011			; inst/del key pressed
	eor #$10
	sta d011
	sta $d011
	rts
incsong	
	dex
	bne decsong
	ldx playnum			; + key pressed
	inx
	cpx playmax
	bcc xstart
	ldx #0
	beq xstart			; bra
decsong
	dex
	bne newsong
	ldx playnum			; - key pressed
	bne maxsong
	ldx playmax
maxsong dex
	jmp xstart
newsong
#endif /* SCREEN */
	dex
xstart	txa
	jmp start			; start new song

key	.byte $ff

keyrow	.byte $7f	;run/stop

#ifdef SCREEN
	.byte $fe	;inst/del
	.byte $df	;+
	.byte $df	;-
#endif /* SCREEN */

	.byte $7f	;1
	.byte $7f	;2
	.byte $fd	;3
	.byte $fd	;4
	.byte $fb	;5
	.byte $fb	;6
	.byte $f7	;7
	.byte $f7	;8
	.byte $ef	;9
	.byte $ef	;0
	.byte $fd	;a
	.byte $f7	;b
	.byte $fb	;c
	.byte $fb	;d
	.byte $fd	;e
	.byte $fb	;f
	.byte $f7	;g
	.byte $f7	;h
	.byte $ef	;i
	.byte $ef	;j
	.byte $ef	;k
	.byte $df	;l
	.byte $ef	;m
	.byte $ef	;n
	.byte $ef	;o
	.byte $df	;p
	.byte $7f	;q
	.byte $fb	;r
	.byte $fd	;s
	.byte $fb	;t
	.byte $f7	;u
	.byte $f7	;v
	.byte $fd	;w
	.byte $fb	;x
	.byte $f7	;y
	.byte $fd	;z
numkeys = * - keyrow

keycol	.byte $80	;run/stop

#ifdef SCREEN
	.byte $01	;inst/del
	.byte $01	;+
	.byte $08	;-
#endif /* SCREEN */

	.byte $01	;1
	.byte $08	;2
	.byte $01	;3
	.byte $08	;4
	.byte $01	;5
	.byte $08	;6
	.byte $01	;7
	.byte $08	;8
	.byte $01	;9
	.byte $08	;0
	.byte $04	;a
	.byte $10	;b
	.byte $10	;c
	.byte $04	;d
	.byte $40	;e
	.byte $20	;f
	.byte $04	;g
	.byte $20	;h
	.byte $02	;i
	.byte $04	;j
	.byte $20	;k
	.byte $04	;l
	.byte $10	;m
	.byte $80	;n
	.byte $40	;o
	.byte $02	;p
	.byte $40	;q
	.byte $02	;r
	.byte $20	;s
	.byte $40	;t
	.byte $40	;u
	.byte $80	;v
	.byte $02	;w
	.byte $80	;x
	.byte $02	;y
	.byte $10	;z
	.)


#ifdef SCREEN

; A bit of CIA register $DC00 (Data Port A) is zero when the joystick connected
; to port two is moved in a certain direction or the fire button is pressed. By
; using the previous state of the joystick it is easy to determine the beginning
; of a new move. In the table below, A is the previous state of $DC00 and B is
; the current state of $DC00.
;
; A  B  (A xor B) and A
; -  -  ---------------
; 0  0  0
; 0  1  0
; 1  0  1
; 1  1  0

joystick .(
	lda #$ff
	sta $dc00
	lda $dc00
	tax
	eor prevjoy
	and prevjoy			; set bits indicate start of new move
	stx prevjoy
	lsr
	bcc tdown
	ldx playnum			; joystick moved up
	inx
	cpx playmax
	bcc xstart
	ldx #0
	beq xstart			; bra
tdown
	lsr
	bcc tleft
	ldx playnum			; joystick moved down
	bne maxsong
	ldx playmax
maxsong dex
xstart	txa
	jmp start			; start new song
tleft
	lsr
	bcc tright
	jmp stop			; joystick moved left
tright
	lsr
	bcc exit
	jmp restart			; joystick moved right
exit
	rts
	.)


clock	.(				; update the clock
	ldx clkcounter
	bmi exit			; clock is not running
	inx
	txa
	ldy video
	cmp clkspd,y
	bcc cl1
	ldx #4
cl2	ldy clkoffs-1,x
	lda screen+CLOCK_OFFSET,y
	adc #0				; c=1
	cmp clkcmp-1,x
	bcc cl3
	lda #$30			; '0'
	.byte $2c			; bit $xxxxx
cl3	ldx #1				; no wraparound, so break the loop
	sta screen+CLOCK_OFFSET,y
	dex
	bne cl2				; c=1
cl1	stx clkcounter

	ldy video			; update playback progress bar
	ldx barspd,y
	ldy bartpi
	tya
	ora bartpi+1
	beq exit			; no songlength info for this song
bc3	inc barcounter
	bne bc1
	inc barcounter+1
bc1	cpy barcounter
	bne bc2
	lda bartpi+1			; c=1
	sbc barcounter+1
	bne bc2
	sta barcounter
	sta barcounter+1
	lda barpos
	cmp #BAR_PIXELS
	bcs bc2
	inc barpos
bc2	dex
	bne bc3
	jsr drawbar
exit
	rts

clkcmp	.byte $3a,$3a,$36,$3a
clkspd	.byte 60,50			; ntsc/pal frames per second
barspd	.byte 5,6			; ntsc/pal ticks per frame
	.)


wrsonglen	.(			; write BCD coded song length part
	pha
	lsr
	lsr
	lsr
	lsr
	jsr wrdigit
	pla
	and #$0f
wrdigit	ora #$30
	sta screen+LENGTH_OFFSET,x
	inx
	rts
	.)


drawbar	.(				; draw playback progress bar
	lda barpos
	tax
	and #$f8			; position sprite for color overlapping
	clc
	adc #(BAR_X*8)+24
	sta $d000
	lda #0
	rol
	sta $d010
	txa
	and #7				; draw sprite
	tay
	lda dbspr,y
	ldy #24
db3	sta screen+BAR_SPRITE_SCREEN_OFFSET-3,y
	dey
	dey
	dey
	bne db3
	txa
	lsr				; set progress bar character colors
	lsr
	lsr
	tax
	lda #<COL_BAR_FG
db1	dex
	bpl db2
	lda #<COL_BAR_BG
db2	sta $d800+BAR_OFFSET,y
	iny
	cpy #BAR_WIDTH
	bne db1
	rts

dbspr	.byte $00,$80,$c0,$e0,$f0,$f8,$fc,$fe
	.)


colorline .(				; moving color line effect
	ldy $d800+4-3
	ldx #0
l1	lda $d800+5-3,x
	sta $d800+4-3,x
	inx
	cpx #31
	bcc l1
	lda $d800+75-4
	sta $d800+35-4
	lda $d800+115-4
	sta $d800+75-4
	ldx #29
l2	lda $d800+84-3,x
	sta $d800+85-3,x
	dex
	bpl l2
	lda $d800+44-3
	sta $d800+84-3
	sty $d800+44-3
	rts
	.)


colortext .(				; flashing color text effect
	inc counter
	lda counter
	lsr
	and #15
	tay
	lda txtcol,y
	ldx #37
l1	sta $d800+961,x
	dex
	bpl l1
	rts

counter	.byte 0

txtcol	.byte <COL_FOOTER_0,<COL_FOOTER_1,<COL_FOOTER_2,<COL_FOOTER_3
	.byte <COL_FOOTER_4,<COL_FOOTER_5,<COL_FOOTER_6,<COL_FOOTER_7
	.byte <COL_FOOTER_8,<COL_FOOTER_9,<COL_FOOTER_10,<COL_FOOTER_11
	.byte <COL_FOOTER_12,<COL_FOOTER_13,<COL_FOOTER_14,<COL_FOOTER_15
	.)

scroller .(

	ldy #>stil
	beq exit			; no stil text
#ifdef SMOOTH_SCROLL
	ldx #38
colscr1	lda $d800+SCROLLER_OFFSET,x
	sta $d801+SCROLLER_OFFSET,x
	dex
	bpl colscr1
	dec d016
	dec d016
#else /* SMOOTH_SCROLL */
	dec counter
#endif /* SMOOTH_SCROLL */
	bpl exit
#ifdef SMOOTH_SCROLL
	lda #6
	sta d016
	inc counter
	lda counter
	and #7
	tax
	lda scrcol,x
	sta $d800+SCROLLER_OFFSET
#else /* SMOOTH_SCROLL */
	lda #5
	sta counter
#endif /* SMOOTH_SCROLL */

	ldx #0
scroll	lda screen+SCROLLER_OFFSET+1,x
	sta screen+SCROLLER_OFFSET,x
	inx
	cpx #39
	bcc scroll

msgpos	= *+1
newchar	ldx eot
eot	= *+1
	cpx #$ff
	bne okchar
	inx				; restart scroll text
	stx msgpos
	sty msgpos+1
	beq newchar			; bra
okchar	stx screen+SCROLLER_OFFSET+39
	inc msgpos
	bne exit
	inc msgpos+1
exit	rts

counter	.byte 0

#ifdef SMOOTH_SCROLL
scrcol	.byte <COL_SCROLLER_0,<COL_SCROLLER_1,<COL_SCROLLER_2,<COL_SCROLLER_3
	.byte <COL_SCROLLER_4,<COL_SCROLLER_5,<COL_SCROLLER_6,<COL_SCROLLER_7
#endif /* SMOOTH_SCROLL */
	.)

#endif /* SCREEN */
